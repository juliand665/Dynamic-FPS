plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'maven-publish'
	id 'org.quiltmc.quilt-mappings-on-loom' version '4.2.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { url = "https://maven.jamieswhiteshirt.com/libs-release/" }
	maven { url = "https://jitpack.io" }
	maven { url = "https://maven.shedaniel.me/" }
	maven { url = "https://maven.terraformersmc.com/releases/" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings(loom.layered {
		addLayer(quiltMappings.mappings("org.quiltmc:quilt-mappings:${minecraft_version}+build.${project.quilt_mappings}:v2"))
	})
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	
	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation fabricApi.module('fabric-resource-loader-v0', project.fabric_version)
	modImplementation fabricApi.module('fabric-rendering-v1', project.fabric_version)
	modImplementation fabricApi.module('fabric-lifecycle-events-v1', project.fabric_version)
	modImplementation fabricApi.module('fabric-key-binding-api-v1', project.fabric_version)
	
	// Mod Menu, to add the hook for a config screen
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
	
	// Easy config screen setup library
	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	
	// Using the TOML config format
	modImplementation("com.moandjiezana.toml:toml4j:${project.toml4j_version}")
	include "com.moandjiezana.toml:toml4j:${project.toml4j_version}"
	
	// Lazy DFU makes the dev env start up much faster by loading DataFixerUpper lazily, which would otherwise take a long time. We rarely need it anyway.
	modRuntimeOnly("com.github.astei:lazydfu:${lazydfu_version}") {
		exclude(module: "fabric-loader")
	}
	
	// Nullability Annotations
	implementation "com.google.code.findbugs:jsr305:${project.findbugs_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// Minecraft 1.18 upwards uses Java 17.
	it.options.release = 17
	
	it.options.compilerArgs += ["-Xlint:deprecation"]
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	exclude '**/*.psd'
	exclude '**/*.afdesign'
	
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
