plugins {
	alias libs.plugins.architectury
	alias libs.plugins.architectury.loom apply false
}

architectury {
	minecraft = libs.versions.minecraft.get()
}

subprojects {
	if (project.name == "platforms") {
		return
	}

	apply plugin: "dev.architectury.loom"

	loom {
		silentMojangMappingsLicense()
	}

	dependencies {
		minecraft libs.minecraft
		mappings loom.officialMojangMappings()
	}
}

allprojects {
	if (project.name == "platforms") {
		return
	}

	apply plugin: "java"
	apply plugin: "architectury-plugin"
	apply plugin: "maven-publish"

	base {
		archivesName = rootProject.archives_base_name
	}

	group = rootProject.maven_group

	if (!project.hasProperty("loom.platform")) {
		version = rootProject.mod_version
	} else {
		var platform = project.property("loom.platform")
		version = "${rootProject.mod_version}+minecraft-${libs.versions.minecraft.get()}-${platform}"
	}

	repositories {
		// Add repositories to retrieve artifacts from in here.
		// You should only use this when depending on other mods because
		// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
		// See https://docs.gradle.org/current/userguide/declaring_repositories.html
		// for more information about repositories.
	}

	java {
		withSourcesJar()

		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.encoding = "UTF-8"

		javaCompiler = javaToolchains.compilerFor {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}
}
