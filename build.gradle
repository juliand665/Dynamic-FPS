plugins {
	alias libs.plugins.architectury
	alias libs.plugins.architectury.loom apply false
}

architectury {
	minecraft = libs.versions.minecraft.get()
}

subprojects {
	if (project.name == "platforms") {
		return
	}

	apply plugin: "java"
	apply plugin: "maven-publish"

	apply plugin: "architectury-plugin"
	apply plugin: "dev.architectury.loom"

	base {
		archivesName = rootProject.archives_base_name
	}

	group = rootProject.maven_group

	if (!project.hasProperty("loom.platform")) {
		version = rootProject.mod_version
	} else {
		var platform = project.property("loom.platform")
		version = "${rootProject.mod_version}+minecraft-${project.minecraft_version_min}-${platform}"
	}

	loom {
		silentMojangMappingsLicense()
	}

	repositories {
		// Battery
		exclusiveContent {
			filter { includeGroup("net.lostluma") }
			forRepository { maven { url = "https://maven.lostluma.net/releases" } }
		}
	}

	dependencies {
		minecraft libs.minecraft
		mappings loom.officialMojangMappings()

		implementation(libs.battery)
	}

	java {
		withSourcesJar()

		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.encoding = "UTF-8"

		javaCompiler = javaToolchains.compilerFor {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}
}
